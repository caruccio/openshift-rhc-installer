; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "OpenShift-RHC"
!define PRODUCT_VERSION "1.0.0-1"
!define PRODUCT_PUBLISHER "OpenShift Origin"
!define PRODUCT_WEB_SITE "http://openshift.com/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include LogicLib.nsh
!include nsDialogs.nsh

!addincludedir .\include
!include EnvVarUpdate.nsh

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "LICENSE"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY

Page custom proxyPage proxyPageLeave
Page custom libraServerPage libraServerPageLeave

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "rhc.bat"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; include for some of the windows messages defines
!include "winmessages.nsh"
; HKLM (all users) vs HKCU (current user) defines
!define env_hklm 'HKLM "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"'
!define env_hkcu 'HKCU "Environment"'

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Install.exe"
XPStyle on

InstallDir "$PROGRAMFILES\openshift-rhc"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show


Var HTTP_PROXY
Var proxyDialog
Var proxyLabel
Var proxyTextBox
Var proxyCheckbox

; dialog create function
Function proxyPage
  
  ; === libra_server (type: Dialog) ===
  nsDialogs::Create 1018
  Pop $proxyDialog
  ${If} $proxyDialog == error
    Abort
  ${EndIf}
  !insertmacro MUI_HEADER_TEXT "Configure HTTP Proxy (for gem install)" ""
  
  ; === Label ===
  ${NSD_CreateLabel} 8u 5u 91u 9u "HTTP Proxy"
  Pop $proxyLabel
  
  ; === TextBox ===
  ${NSD_CreateText} 8u 16u 124u 11u ""
  Pop $proxyTextBox
  
  ; === Checkbox ===
  ${NSD_CreateCheckbox} 8u 45u 250u 11u "&is OpenShift Server behind the proxy?"
  Pop $proxyCheckbox
  nsDialogs::Show $proxyDialog
FunctionEnd

; dialog leave function
Function proxyPageLeave
  ${NSD_GetText} $proxyTextBox $HTTP_PROXY 
  
  ${NSD_GetState} $proxyCheckbox $0
  ${If} $0 <> 0
	WriteRegExpandStr ${env_hklm} "LIBRA_SERVER_PROXY" "$HTTP_PROXY"
  ${Else}
	WriteRegExpandStr ${env_hklm} "LIBRA_SERVER_PROXY" ""
  ${EndIf}
FunctionEnd


Var libraServerDialog
Var libraServerLabel
Var getupLibraServerRadio
Var redhatLibraServerRadio
Var customLibraServerRadio
Var customLibraServerText

; dialog create function
Function libraServerPage
  
  ; === libra_server (type: Dialog) ===
  nsDialogs::Create 1018
  Pop $libraServerDialog
  ${If} $libraServerDialog == error
    Abort
  ${EndIf}
  !insertmacro MUI_HEADER_TEXT "Configure OpenShift RHC" ""
  
  ; === Label ===
  ${NSD_CreateLabel} 8u 5u 91u 9u "OpenShift Broker URL:"
  Pop $libraServerLabel

  ; === RadioButton ===
  ${NSD_CreateRadioButton} 8u 16u 300u 11u "Getup Cloud's OpenShift (https://broker.getupcloud.com)"
  Pop $getupLibraServerRadio
  ${NSD_Check} $getupLibraServerRadio

  ${NSD_CreateRadioButton} 8u 32u 300u 11u "Red Hat's OpenShift Online (https://openshift.redhat.com)"
  Pop $redhatLibraServerRadio

  ${NSD_CreateRadioButton} 8u 48u 38u 11u "Custom:"
  Pop $customLibraServerRadio
  ${NSD_CreateText} 50u 48u 150u 11u ""
  Pop $customLibraServerText

  nsDialogs::Show $libraServerDialog
FunctionEnd

Var libraServerState
Var libraServerURL

; dialog leave function
Function libraServerPageLeave

  ${NSD_GetText} $customLibraServerText $libraServerURL

  ${NSD_GetState} $getupLibraServerRadio $0
  ${If} $0 == ${BST_CHECKED}
    StrCpy $libraServerURL "https://broker.getupcloud.com"
  ${EndIf}

  ${NSD_GetState} $redhatLibraServerRadio $0
  ${If} $0 == ${BST_CHECKED}
    StrCpy $libraServerURL "https://openshift.redhat.com"
  ${EndIf}

  ${NSD_GetState} $customLibraServerRadio $0
  ${If} $0 == ${BST_CHECKED}
    ${NSD_GetText} $customLibraServerText $libraServerURL
  ${EndIf}

  ${If} $libraServerURL == ""
    MessageBox MB_OK "Please, select one option or insert a valid OpenShift broker URL."
  ${EndIf}

  ; set variable
  WriteRegExpandStr ${env_hklm} "LIBRA_SERVER" "$libraServerURL"
FunctionEnd

Function .onInit
  !insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "copy files" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "rhc"
  File "rhc.bat"
  CreateDirectory "$SMPROGRAMS\OpenShift"
  CreateShortCut "$SMPROGRAMS\OpenShift\rhc.lnk" "$INSTDIR\rhc.bat"
  CreateShortCut "$DESKTOP\rhc.lnk" "$INSTDIR\rhc.bat"
SectionEnd

Section "install git bash" SEC02
  SetOutPath "$INSTDIR"
  ; download and install
  NSISdl::download "https://msysgit.googlecode.com/files/Git-1.8.1.2-preview20130201.exe" "gitinstaller.exe"
  Pop $R0
  ${If} $R0 == 'success'
    ; download successful
    StrCpy $0 ''
    ; run the one click installer
    ExecWait '"gitinstaller.exe" /silent /nocancel /noicons' $0
    ${If} $0 == ''
	  ; execution of one click installer failed
	  MessageBox MB_OK "git install failed."
    ${EndIf}
	Delete "$INSTDIR\gitinstaller.exe"
  ${Else}
    ; download not successfull
    MessageBox MB_OK "git download failed."
  ${EndIf}
SectionEnd

Section "install ruby" SEC03
  SetOutPath "$INSTDIR"
  ; download and install
  NSISdl::download "http://rubyforge.org/frs/download.php/76952/rubyinstaller-1.9.3-p429.exe" "rubyinstaller.exe"
  Pop $R0
  ${If} $R0 == 'success'
    ; download successful
    StrCpy $0 ''
    ; run the one click installer
    ExecWait '"rubyinstaller.exe" /verysilent /noreboot /nocancel /noicons /dir="$INSTDIR/ruby"' $0
    ${If} $0 == ''
	  ; execution of one click installer failed
	  MessageBox MB_OK "ruby install failed."
    ${EndIf}
	Delete "$INSTDIR\rubyinstaller.exe"
  ${Else}
    ; download not successfull
    MessageBox MB_OK "ruby download failed."
  ${EndIf}
SectionEnd

Section "install rhc" SEC04
  SetOutPath "$INSTDIR"
   
  StrCpy $1 ''
  ; install rhc locally
  ${If} $HTTP_PROXY == ''
    ExecWait '"$INSTDIR\ruby\bin\gem.bat" install rhc --no-rdoc --no-ri' $1
  ${Else}
    ExecWait '"$INSTDIR\ruby\bin\gem.bat" install --http-proxy "$HTTP_PROXY" rhc --no-rdoc --no-ri' $1
  ${EndIf}
  ${If} $1 == ''
    MessageBox MB_OK "Gem install failed. Please install the rhc gem manually"
  ${EndIf}
SectionEnd

Section -AdditionalIcons
  CreateShortCut "$SMPROGRAMS\rhc\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\rubyinstaller.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\rubyinstaller.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  ${EnvVarUpdate} $0 "PATH" "A" "HKCU" "$INSTDIR"  
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "You have successfully uninstalled $(^Name)."
FunctionEnd

Function un.onInit
!insertmacro MUI_UNGETLANGUAGE
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\rhc.bat"
  Delete "$INSTDIR\rhc"
  RMDir /r "$INSTDIR\ruby"

  Delete "$SMPROGRAMS\rhc\Uninstall.lnk"
  Delete "$SMPROGRAMS\rhc\Website.lnk"
  Delete "$DESKTOP\rhc.lnk"
  Delete "$SMPROGRAMS\rhc\rhc.lnk"

  RMDir "$SMPROGRAMS\rhc"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
  ${un.EnvVarUpdate} $0 "PATH" "R" "HKCU" "$INSTDIR"  
SectionEnd
